plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'org.springframework.boot' version '2.6.2' apply false
}

configure(allprojects) { project ->

    apply plugin: "io.spring.dependency-management"

    ext {
        bouncycastleVersion = '1.68'
        flyingSaucerVersion = '9.1.20'
        hutoolVersion = '5.7.22'
        javafakerVersion = '1.0.2'
        mybatisVersion = '3.5.9'
        mybatisPlusVersion = '3.5.1'
        redissonVersion = '3.17.0'
        springSecurityVersion = '5.6.1'
        springBootVersion = '2.6.2'
    }
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.security:spring-security-bom:${springSecurityVersion}"
        }
        dependencies {
            dependencySet(group: 'org.bouncycastle', version: bouncycastleVersion) {
                entry 'bcprov-jdk15on'
                entry 'bcmail-jdk15on'
            }

            dependency "com.github.javafaker:javafaker:${javafakerVersion}"

        }
    }


    repositories {
        mavenLocal()

        maven {
            url = 'https://maven.aliyun.com/repository/public'
        }
        maven {
            url = uri('https://repository.jboss.org/nexus/content/groups/public-jboss/')
        }

    }



    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, "seconds"
            cacheDynamicVersionsFor 0, "seconds"
        }
    }

}


subprojects {

    apply plugin: 'java-library'


    tasks.withType(JavaCompile) {
        options.warnings = true
        options.encoding = 'UTF-8'
        options.compilerArgs += ['-parameters', '-Xdoclint:none', '-Xlint:none', '-nowarn']
    }


    configurations{
        implementation.canBeResolved = true
        api.canBeResolved = true
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17

        registerFeature('optional') {
            usingSourceSet(sourceSets.main)
        }
    }


    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
    group = 'cn.keking'
    version = '4.1.0-SNAPSHOT'

}




